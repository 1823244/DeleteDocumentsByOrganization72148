
Процедура ВыполнитьУдалениеДокументов(ДатаНач, ДатаКон, Организация, Непосредственно = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);			   
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Для Каждого Д из Метаданные.Документы Цикл
		ОбработкаПрерыванияПользователя();
		
		Если Д.Реквизиты.Найти("Организация")=Неопределено Тогда
			продолжить;
		КонецЕсли;	
		
		//ну да, запрос в цикле, а что делать :)
		Запрос.Текст = "ВЫБРАТЬ
		               |	Док.Ссылка
		               |ИЗ
		               |	Документ.ЧекККМ КАК Док
		               |ГДЕ
		               |	Док.Дата МЕЖДУ &ДатаНач И &ДатаКон
		               |	И Док.Организация = &Организация
		               |УПОРЯДОЧИТЬ ПО
		               |	Док.Дата";
					   
		//Это чтобы можно быть текст запрос открывать в конструкторе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ЧекККМ",Д.Имя);
		
		ТЗ = Запрос.Выполнить().Выгрузить();
		Если ТЗ.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Сообщить("Удаляются документы вида "+Д.Синоним);
		
		Для Каждого Стр Из ТЗ Цикл
			ОбработкаПрерыванияПользователя();
			
			ДокОбъект = Стр.Ссылка.ПолучитьОбъект();
			состояние("Удаляются документы за "+Строка(ДокОбъект.Дата));
			
			Если Непосредственно Тогда
				ДокОбъект.Удалить();
			Иначе	
				ДокОбъект.УстановитьПометкуУдаления(Истина);
			КонецЕсли;	
			
		КонецЦикла;	
		
		
	КонецЦикла;	 //по документам из метаданных
	
КонецПроцедуры
